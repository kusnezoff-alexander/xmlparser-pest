// Character class (inspired by [Github](https://github.com/jturner314/py_literal/blob/master/src/grammar.pest)
Char = @{ "\x09" | "\x0A" | "\x0D" | '\x20'..'\u{D7FF}' | '\u{E000}'..'\u{FFFD}' | '\u{10000}'..'\u{10FFFF}' }

S = { ("\x20" | "\x09" | "\x0D" | "\x0A")+ } // see [S](https://www.w3.org/TR/REC-xml/#NT-S)
Quote = { "'" | "\"" }

// Comment [15](https://www.w3.org/TR/REC-xml/#NT-Comment)
Comment = { "<!--" ~ (!"-" ~ Char | "-" ~ !"-" ~ Char)* ~ "-->" } // =last char can't be '-'

// # Prolog [22]
XMLDecl = { "<?xml" ~ VersionInfo ~ EncodingDecl? ~ StandaloneDecl? ~ S? ~ "?>" } // [23](https://www.w3.org/TR/REC-xml/#NT-XMLDecl)
VersionInfo = { S ~ "version" ~ Eq ~ PUSH(Quote) ~ VersionNum ~ POP }
Eq = { S? ~ "=" ~ S? } // [25]
VersionNum = { "1." ~ ASCII_DIGIT+ } // [VerionNum](https://www.w3.org/TR/REC-xml/#NT-VersionNum): Warning if !=1.0?

EncodingDecl = { S ~ "encoding" ~ Eq ~ PUSH(Quote) ~ EncName ~ POP} // [80](https://www.w3.org/TR/REC-xml/#NT-EncodingDecl)
EncName = { ASCII_ALPHA  ~ ( ASCII_ALPHANUMERIC | "." | "_" | "-" )* } // [81] = only *Latin Letters*

// `standalone="yes"` == ignore any markup declarations in the DTD
// see [StackOverflow](https://stackoverflow.com/a/14690849/20675205)
StandaloneDecl = { S ~ "standalone" ~ Eq ~ PUSH(Quote) ~ ("yes"|"no") ~ POP } // [32](https://www.w3.org/TR/REC-xml/#NT-SDDecl)

// # CDATA Sections
CDStart = { "<![CDATA[" }
CDEnd = { "]]>" }
CData = { (!("]]>") ~ Char )* }
CDSect = { CDStart ~ CData ~ CDEnd }


